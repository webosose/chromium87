# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//neva/neva.gni")

if (use_neva_media) {
  mojom("mojom") {
    sources = [ "neva/mojom/video_window_controller.mojom" ]
    public_deps = [
      "//mojo/public/mojom/base",
      "//ui/gfx/geometry/mojom",
      "//ui/gfx/mojom",
    ]

    cpp_typemaps = [
      {
        types = [
          {
            mojom = "ui.mojom.VideoWindowInfo"
            cpp = "::ui::VideoWindowInfo"
          },
          {
            mojom = "ui.mojom.VideoWindowParams"
            cpp = "::ui::VideoWindowParams"
          },
        ]
        traits_headers = [
          "neva/video_window_info.h",
          "neva/mojom/video_window_controller_mojom_traits.h",
        ]
        traits_sources =
            [ "neva/mojom/video_window_controller_mojom_traits.cc" ]
        traits_public_deps = [ "//base" ]
      },
    ]
  }
}

component("platform_window") {
  sources = [
    "extensions/workspace_extension.cc",
    "extensions/workspace_extension.h",
    "extensions/workspace_extension_delegate.h",
    "platform_window.cc",
    "platform_window.h",
    "platform_window_delegate.cc",
    "platform_window_delegate.h",
    "platform_window_init_properties.cc",
    "platform_window_init_properties.h",
  ]

  defines = [ "IS_PLATFORM_WINDOW_IMPL" ]

  public_deps = [ "//ui/base/cursor:cursor_base" ]

  deps = [
    "//base",
    "//ui/base",
    "//ui/gfx",
  ]

  #@name USE_NEVA_APPRUNTIME
  #@{
  sources += [
    "neva/platform_window.h",
    "neva/platform_window_delegate.h",
    "neva/ui_utils.cc",
    "neva/ui_utils.h",
    "neva/window_group_configuration.cc",
    "neva/window_group_configuration.h",
    "neva/window_group_configuration_param_traits.cc",
    "neva/window_group_configuration_param_traits.h",
    "neva/window_group_configuration_param_traits_macros.h",
    "neva/window_group_ipc_export.h",
    "neva/xinput_types.h",
  ]

  deps += [ "//ipc" ]

  #@}

  if (use_neva_media) {
    sources += [
      "neva/video_window_controller.h",
      "neva/video_window_controller_host.h",
    ]
    deps += [ "//ui/platform_window:mojom" ]
  }

  if (is_fuchsia) {
    sources += [
      "fuchsia/initialize_presenter_api_view.cc",
      "fuchsia/initialize_presenter_api_view.h",
    ]

    public_deps += [
      "//third_party/fuchsia-sdk/sdk/fidl/fuchsia.ui.policy",
      "//third_party/fuchsia-sdk/sdk/fidl/fuchsia.ui.views",
      "//third_party/fuchsia-sdk/sdk/pkg/scenic_cpp",
      "//third_party/fuchsia-sdk/sdk/pkg/sys_cpp",
    ]
  }

  if (is_linux || is_chromeos) {
    sources += [
      "extensions/wayland_extension.cc",
      "extensions/wayland_extension.h",
      "extensions/x11_extension.cc",
      "extensions/x11_extension.h",
      "extensions/x11_extension_delegate.h",
    ]
  }
}

group("platform_impls") {
  public_deps = [ "//ui/platform_window/stub" ]
  if (use_x11) {
    public_deps += [ "//ui/platform_window/x11" ]
  } else if (is_win) {
    public_deps += [ "//ui/platform_window/win" ]
  }
}
